configure(allprojects) { project ->
	group = PROJ_GROUP
	version = PROJ_VERSION
	ext.bboss_version = BBOSS_PROJ_VERSION
	ext.mongodbversion = mongodbversion
	ext.jacksonversion = jacksonversion
	apply plugin: "java"
	apply plugin: "maven-publish"
	apply plugin: "eclipse"
	apply plugin: "idea"
	apply plugin: "signing"
	apply plugin: "java-library"
	if(project.getProperty('skipTest'))
	{
		compileTestJava.enabled=false
		processTestResources.enabled=false
		testClasses.enabled = false
		//test.onlyIf { !project.getProperty('skipTest') } 
		test.enabled = false  
	}
	eclipse {
		jdt {
			//if you want to alter the java versions (by default they are configured with gradle java plugin settings):
			sourceCompatibility = SOURCE_COMPILER_LEVEL
			targetCompatibility = TARGET_COMPILER_LEVEL
			javaRuntimeName = "../../org.eclipse.jdt.launching.JRE_CONTAINER"

		}
	}

	tasks.withType(JavaCompile) {	
		sourceCompatibility = SOURCE_COMPILER_LEVEL
		targetCompatibility = TARGET_COMPILER_LEVEL
		options.encoding = 'UTF-8'
	}

	tasks.withType(Javadoc) {	
		options.encoding = 'UTF-8'
		// disable the crazy super-strict doclint tool in Java 8
        // noinspection SpellCheckingInspection
		if (JavaVersion.current().isJava8Compatible()) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}

	jar {
		manifest {
			attributes (
					'Implementation': archiveVersion,
					'Specification-Version': archiveVersion,
					'Implementation-Vendor': 'bboss',
					'Implementation-ProductID': project.name,
					'Compile-Timestamp': new Date().format('yyyy-MM-dd HH:mm:ss'),
					'Compile-User': DEVELOPER_NAME
			)
		}
	}

	dependencies {
		testImplementation 'junit:junit:4.12'
		 compileOnly(
    		 fileTree(dir: '../lib-compile', include: '**/*.jar'),    
   		 )
   		 api(
   		 //  [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.2', transitive: false],
         //  	[group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.2', transitive: false],
        //  [group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.2', transitive: false],
                //add on 20170223 begin
          [group: 'com.bbossgroups', name: 'bboss-http', version: "${BBOSS_HTTP_VERSION}", transitive: true],
   		 )
   		  
	}

	repositories {
		mavenLocal()		
		 maven {
	        url "https://maven.aliyun.com/repository/public"
	    }	    
		mavenCentral()
		
	}
}

configure(subprojects) { subproject ->
	task sourcesJar(type: Jar) {
		archiveClassifier = "sources"
		from sourceSets.main.allJava
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}
	processResources{
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}
	task javaDocJar(type: Jar, dependsOn: javadoc) {
		archiveClassifier = "javadoc"
		from javadoc.destinationDir
	}
	artifacts {
		archives sourcesJar, javaDocJar
	}

	 
}
		 

configure([project(":bboss-pdp-auth"),project(":bboss-pdp-adminservice"),project(":bboss-pdp-dict"),project(":bboss-pdp-fastwx"),project(":bboss-pdp-dubbo-inf"),project(":bboss-pdp-dubbo")]) { subproject ->
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId project.group
				artifactId project.name
				version "${version}"
				from components.java
				artifact sourcesJar
				artifact javaDocJar
//				versionMapping {
//					usage('java-api') {
//						fromResolutionOf('runtimeClasspath')
//					}
//					usage('java-runtime') {
//						fromResolutionResult()
//					}
//				}
//				from components.java
				pom {
					name = project.name
					packaging = 'jar'
					// optionally artifactId can be defined here
					description = PROJ_DESCRIPTION
					url = PROJ_WEBSITEURL

					scm {
						connection = scm_connection
						developerConnection = scm_developerConnection
						url = scm_url
					}

					licenses {
						license {
							name = PROJ_LICENCE_NAME
							url = PROJ_LICENCE_URL
						}
					}

					developers {
						developer {
							id = DEVELOPER_ID
							name = DEVELOPER_NAME
							email = DEVELOPER_EMAIL
						}
					}

				}
			}
		}
		repositories {
//			maven {
//				// change URLs to point to your repos, e.g. http://my.org/repo
//				def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
//				def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
//				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
//			}
			maven {
				// change URLs to point to your repos, e.g. http://my.org/repo
				allowInsecureProtocol = true
				url = sonatype_url
				credentials {
					username = sonatype_username
					password = sonatype_password
				}

			}


		}
	}

	signing {
		sign publishing.publications.mavenJava
//		sign configurations.archives
	}
}//configure


configure([project(":bboss-pdp-web"),project(":bboss-pdp-config")]) { subproject ->
	apply plugin: 'war'    
	eclipse {
		jdt {
			//if you want to alter the java versions (by default they are configured with gradle java plugin settings):
			sourceCompatibility = 1.7
			targetCompatibility = 1.7
			javaRuntimeName = "../../org.eclipse.jdt.launching.JRE_CONTAINER"

		}
	}
	
	dependencies {
	   
	 	api project(':bboss-pdp-auth')
	   
	  
	   compileOnly (            
	          
	            [group: 'javax.servlet', name: 'javax.servlet-api', version: "3.0.1", transitive: false],
	            //[group: 'javax.servlet', name: 'jsp-api', version: '2.0', transitive: false],
	            [group: 'javax.servlet.jsp', name: 'javax.servlet.jsp-api', version: '2.3.1', transitive: false],
	          
	    	)
	    
	   
	}
	/**war {
	    //from 'WebRoot'
	    // adds a file-set to the root of the archive
	    //webInf { from 'WebRoot/WEB-INF' } // adds a file-set to the WEB-INF dir.
	    //classpath configurations.compile // adds a configuration to the WEB-INF/lib dir.
	   // webXml = file('WebRoot/WEB-INF/web.xml') // copies a file to WEB-INF/web.xml
	}*/
    war {
        webAppDirectory = file('WebRoot')
    } 
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				artifact war
			}
		}
		repositories {
			mavenLocal()
		}
	}
}